Lively Sandstone Vulture

medium

# Calls to `updateVestingPoints` can lock out users from claiming their vested tokens.

## Summary

Calls to [`updateVestingPoints(uint128[2][])`](https://github.com/sherlock-audit/2024-03-zap-protocol/blob/c2ad35aa844899fa24f6ed0cbfcf6c7e611b061a/zap-contracts-labs/contracts/Vesting.sol#L94C14-L94C33) can inadvertently punish rightful claimants to the point of DoS.

## Vulnerability Detail

The vesting schedule for points in [`Vesting`](https://github.com/sherlock-audit/2024-03-zap-protocol/blob/c2ad35aa844899fa24f6ed0cbfcf6c7e611b061a/zap-contracts-labs/contracts/Vesting.sol) can be changed arbitrarily, however a claimant's interaction with their vested tokens is inextricably linked with the length of the _currently signalled_ vesting points array. This relationship is fundamentally incompatible with changes in length to that array, and to a lesser extent, the modification to array contents that can be caused by a call to [`updateVestingPoints(uint128[2][])`](https://github.com/sherlock-audit/2024-03-zap-protocol/blob/c2ad35aa844899fa24f6ed0cbfcf6c7e611b061a/zap-contracts-labs/contracts/Vesting.sol#L94C14-L94C33).

Let's take a look at a section of the [`claim`](https://github.com/sherlock-audit/2024-03-zap-protocol/blob/c2ad35aa844899fa24f6ed0cbfcf6c7e611b061a/zap-contracts-labs/contracts/Vesting.sol#L67) function:

```solidity
function claim() external {
    // ...
    uint256 pctAmount;
    uint256 i = s.index;
    for (i; i <= vestingPoints.length - 1; i++) {
        if (block.timestamp >= vestingPoints[i][0]) {
            pctAmount += (s.userDeposit * vestingPoints[i][1]) / 10000;
        } else {
            break;
        }
    }
    if (pctAmount != 0) {
        // ...
        s.index = uint128(i); /// @audit latched_iteration_index
        s.amountClaimed += pctAmount;
    }
}
```

Notice here that during a call to [`claim`](https://github.com/sherlock-audit/2024-03-zap-protocol/blob/c2ad35aa844899fa24f6ed0cbfcf6c7e611b061a/zap-contracts-labs/contracts/Vesting.sol#L67), we iterate through the time-ascending array of [`vestingPoints`](https://github.com/sherlock-audit/2024-03-zap-protocol/blob/c2ad35aa844899fa24f6ed0cbfcf6c7e611b061a/zap-contracts-labs/contracts/Vesting.sol#L13) to find which threshold for vested tokens have so far been unlocked for the given `block.timestamp`.

For any token unlock thresholds in the [`vestingPoints`](https://github.com/sherlock-audit/2024-03-zap-protocol/blob/c2ad35aa844899fa24f6ed0cbfcf6c7e611b061a/zap-contracts-labs/contracts/Vesting.sol#L13) which have yet to be unlocked (i.e. they are pending future unlock), the iteration `break`s and the maximum index the caller was able to iterate up to is latched to contract storage. The remaining indices can be iterated through on a future call once they are ready to be claimed.

This iteration latching mechanism is designed to ensure that in a future calls to [`claim`](https://github.com/sherlock-audit/2024-03-zap-protocol/blob/c2ad35aa844899fa24f6ed0cbfcf6c7e611b061a/zap-contracts-labs/contracts/Vesting.sol#L67), previously unlocked thresholds cannot be double spent, and works perfectly well for arrays which are not liable to change in future.

However, not all [`vestingPoints`](https://github.com/sherlock-audit/2024-03-zap-protocol/blob/c2ad35aa844899fa24f6ed0cbfcf6c7e611b061a/zap-contracts-labs/contracts/Vesting.sol#L13) are equal, and changes to this array can inadvertently lead to a variety of issues compared to the latched iteration progress of individual users.

```solidity
function updateVestingPoints(
    uint128[2][] memory _vestingPoints
) external onlyRole(DEFAULT_ADMIN_ROLE) {
    uint256 sum;
    (vestingPoints, sum) = ascendingSort(_vestingPoints);
    require(sum == 10000, "sum not 10000"); /// @audit maybe_already_partially_distributed
    require(block.timestamp <= _vestingPoints[0][0], "Time lapsed"); /// @audit old_schedule_possibly_already_active
}
```

> [!TIP]
>
> It should be emphasised that the intended prevention mechanism of `revert`ing the call to [`updateVestingPoints(uint128[2][])`](https://github.com/sherlock-audit/2024-03-zap-protocol/blob/c2ad35aa844899fa24f6ed0cbfcf6c7e611b061a/zap-contracts-labs/contracts/Vesting.sol#L94C14-L94C33) based upon the `block.timestamp` check is insufficient.
>
> The `block.timestamp` check is made against the _proposed_ vesting schedule, not the _current_ vesting schedule. This means some points may have already been distributed, yet the caller is forced to define a full reallocation (`10_000`).

Below, we'll theorize about calls to [`updateVestingPoints(uint128[2][])`](https://github.com/sherlock-audit/2024-03-zap-protocol/blob/c2ad35aa844899fa24f6ed0cbfcf6c7e611b061a/zap-contracts-labs/contracts/Vesting.sol#L94C14-L94C33) which intend on transitioning from an `oldVestingPoints` array to a `newVestingPoints` array. Depending on the configuration of those arrays, the following issues can be observed:

### Scenario One

If we imagine `oldVestingPoints.length == newVestingPoints.length`, but the array children are not identical, users who partially-claimed vesting points may suffer a loss if previously unlocked indices in `oldVestingPoints` subsequently become more valuable in `newVestingPoints`.

This is because they may have previously claimed up to an index which, in `newVestingPoints`, offers a greater number of tokens than what was originally allocated up to the same index back in `oldVestingPoints`, but it is not possible to go back and claim them.

### Scenario Two

If `oldVestingPoints.length > newVestingPoints.length` and a previously partially-claimed vesting points is at an index that is already _greater_ than `newVestingPoints.length`, a user who is still owed vesting points can be locked out entirely from receiving their full token allocation.

This failing occurs because the code will incorrectly assume the claimant to have _already_ spent the majority (if not all) of their `newVestingPoints` allocation, when in fact they had only spent a subset of the previous vesting schedule.

## Impact

Medium.

## Code Snippet

```solidity
function claim() external {
    address sender = msg.sender;

    UserDetails storage s = userdetails[sender];
    require(s.userDeposit != 0, "No Deposit");
    require(s.index != vestingPoints.length, "already claimed");
    uint256 pctAmount;
    uint256 i = s.index;
    for (i; i <= vestingPoints.length - 1; i++) {
        if (block.timestamp >= vestingPoints[i][0]) {
            pctAmount += (s.userDeposit * vestingPoints[i][1]) / 10000;
        } else {
            break;
        }
    }
    if (pctAmount != 0) {
        if (address(token) == address(1)) {
            (bool sent, ) = payable(sender).call{value: pctAmount}("");
            require(sent, "Failed to send BNB to receiver");
        } else {
            token.safeTransfer(sender, pctAmount);
        }
        s.index = uint128(i);
        s.amountClaimed += pctAmount;
    }
}
```

## Tool used

Manual Review

## Recommendation

Do not allow callers to modify indices of [`vestingPoints`](https://github.com/sherlock-audit/2024-03-zap-protocol/blob/c2ad35aa844899fa24f6ed0cbfcf6c7e611b061a/zap-contracts-labs/contracts/Vesting.sol#L13) when the `block.timestamp` has already exceeded the vesting threshold. In addition, the newly proposed vesting schedule should only be able to configure the _remaining_ token allocation, not issue another complete allocation on top of what's already been distributed.
