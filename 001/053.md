Warm Gingerbread Kookaburra

high

# User can drain the Vesting contract via re-entrancy

## Summary
User can steal all funds within the Vesting contract.

## Vulnerability Detail
Within the `claim` function, the user can claim their vested amount, based on which VestingPoints have already passed.
```solidity
    function claim() external {
        address sender = msg.sender;

        UserDetails storage s = userdetails[sender];
        require(s.userDeposit != 0, "No Deposit");
        require(s.index != vestingPoints.length, "already claimed");
        uint256 pctAmount;
        uint256 i = s.index;
        for (i; i <= vestingPoints.length - 1; i++) {
            if (block.timestamp >= vestingPoints[i][0]) {
                pctAmount += (s.userDeposit * vestingPoints[i][1]) / 10000;
            } else {
                break;
            }
        }
        if (pctAmount != 0) {
            if (address(token) == address(1)) {
                (bool sent, ) = payable(sender).call{value: pctAmount}("");
                require(sent, "Failed to send BNB to receiver");
            } else {
                token.safeTransfer(sender, pctAmount);
            }
            s.index = uint128(i);
            s.amountClaimed += pctAmount;
        }
    }
```
The problem is that the user's index is updated after transferring the funds. Meaning that if native token is used for vesting, the user will be able to re-enter in the function endless times and steal all funds within the contract.

The function also lacks a non-reentrant modifier

## Impact
Draining all funds within the Vesting contract

## Code Snippet
https://github.com/sherlock-audit/2024-03-zap-protocol/blob/main/zap-contracts-labs/contracts/Vesting.sol#L89

## Tool used

Manual Review

## Recommendation
Update the user's index before sending them their funds.
