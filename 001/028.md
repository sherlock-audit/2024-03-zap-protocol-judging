Square Butter Lobster

high

# Tokens deposited in the Vesting contract can be drained by re-entering Vesting.claim

## Summary

Tokens deposited in the Vesting contract can be drained by re-entering `Vesting.claim`.

## Vulnerability Detail

`Vesting.claim` does not follow the checks-effects-interactions pattern, making it possible for a re-entry attack. When the registered token address is `address(1)`, it sends native tokens. Users using a contract-based wallet can re-enter the `Vesting.claim` function. Because `s.index` is updated after the `call`, tokens can be claimed again in the re-entered `Vesting.claim`.

```solidity
function claim() external {
    address sender = msg.sender;

    UserDetails storage s = userdetails[sender];
    require(s.userDeposit != 0, "No Deposit");
    require(s.index != vestingPoints.length, "already claimed");
    uint256 pctAmount;
@>  uint256 i = s.index;
    for (i; i <= vestingPoints.length - 1; i++) {
        if (block.timestamp >= vestingPoints[i][0]) {
            pctAmount += (s.userDeposit * vestingPoints[i][1]) / 10000;
        } else {
            break;
        }
    }
    if (pctAmount != 0) {
        if (address(token) == address(1)) {
@>          (bool sent, ) = payable(sender).call{value: pctAmount}("");
            require(sent, "Failed to send BNB to receiver");
        } else {
            token.safeTransfer(sender, pctAmount);
        }
@>      s.index = uint128(i);
        s.amountClaimed += pctAmount;
    }
}
```

## Impact

Tokens deposited in the Vesting contract can be drained through re-entry.

## Code Snippet

[https://github.com/sherlock-audit/2024-03-zap-protocol/blob/c2ad35aa844899fa24f6ed0cbfcf6c7e611b061a/zap-contracts-labs/contracts/Vesting.sol#L83-L89](https://github.com/sherlock-audit/2024-03-zap-protocol/blob/c2ad35aa844899fa24f6ed0cbfcf6c7e611b061a/zap-contracts-labs/contracts/Vesting.sol#L83-L89)

## Tool used

Manual Review

## Recommendation

```diff
function claim() external {
    address sender = msg.sender;

    UserDetails storage s = userdetails[sender];
    require(s.userDeposit != 0, "No Deposit");
    require(s.index != vestingPoints.length, "already claimed");
    uint256 pctAmount;
    uint256 i = s.index;
    for (i; i <= vestingPoints.length - 1; i++) {
        if (block.timestamp >= vestingPoints[i][0]) {
            pctAmount += (s.userDeposit * vestingPoints[i][1]) / 10000;
        } else {
            break;
        }
    }
    if (pctAmount != 0) {
+       s.index = uint128(i);
+       s.amountClaimed += pctAmount;
        if (address(token) == address(1)) {
            (bool sent, ) = payable(sender).call{value: pctAmount}("");
            require(sent, "Failed to send BNB to receiver");
        } else {
            token.safeTransfer(sender, pctAmount);
        }
-       s.index = uint128(i);
-       s.amountClaimed += pctAmount;
    }
}
```