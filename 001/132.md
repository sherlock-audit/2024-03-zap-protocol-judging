Shambolic Lace Giraffe

high

# Vesting claim re-entrancy

## Summary

`claim` in `Vesting` contract does not protect from re-entrancy, opening a gap to claim allocated tokens multiple times.

## Vulnerability Detail

When claiming the vesting tokens, it first calculates the `pctAmount`, then sends it, and finally updates the state:
```solidity
    function claim() external {
        address sender = msg.sender;

        UserDetails storage s = userdetails[sender];
        require(s.userDeposit != 0, "No Deposit");
        require(s.index != vestingPoints.length, "already claimed");
        uint256 pctAmount;
        uint256 i = s.index;
        for (i; i <= vestingPoints.length - 1; i++) {
            if (block.timestamp >= vestingPoints[i][0]) {
                pctAmount += (s.userDeposit * vestingPoints[i][1]) / 10000;
            } else {
                break;
            }
        }
        if (pctAmount != 0) {
            if (address(token) == address(1)) {
                (bool sent, ) = payable(sender).call{value: pctAmount}("");
                require(sent, "Failed to send BNB to receiver");
            } else {
                token.safeTransfer(sender, pctAmount);
            }
            s.index = uint128(i);
            s.amountClaimed += pctAmount;
        }
    }
```
This does not follow the Checks Effects Interactions (CEI) pattern. If the token is native (`address(1)`), or ERC20 that contains a callback hook, then on transfer the execution can re-enter to claim the same amount again and again until the contract is drained.

## Impact

The vesting contract is susceptible to re-entrancy attack and under the right circumstances can be entirely drained.

## Code Snippet

https://github.com/sherlock-audit/2024-03-zap-protocol/blob/main/zap-contracts-labs/contracts/Vesting.sol#L83-L90

## Tool used

Manual Review

## Recommendation

Re-order lines to follow CEI pattern or add re-entrancy guards.
