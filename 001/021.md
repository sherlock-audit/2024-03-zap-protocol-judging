Agreeable Blood Yeti

high

# Reentrancy when claiming vesting funds allows attacker to steal all other users' funds

## Summary
Improper CEI pattern when claiming vesting amount allows a user to drain the contract using reentrancy.
## Vulnerability Detail
Users' claimable vesting funds are [first pushed in](https://github.com/sherlock-audit/2024-03-zap-protocol/blob/c2ad35aa844899fa24f6ed0cbfcf6c7e611b061a/zap-contracts-labs/contracts/Vesting.sol#L53-L65) the contract by the `distributionWallet`. Following that, users can call the `claim()` function to receive portions of their funds if `block.timestamp` has [reached a checkpoint](https://github.com/sherlock-audit/2024-03-zap-protocol/blob/c2ad35aa844899fa24f6ed0cbfcf6c7e611b061a/zap-contracts-labs/contracts/Vesting.sol#L75-L77) in the vesting points array:

```solidity
    function claim() external {
        address sender = msg.sender;

        UserDetails storage s = userdetails[sender];
        require(s.userDeposit != 0, "No Deposit");
        require(s.index != vestingPoints.length, "already claimed");
        uint256 pctAmount;
        uint256 i = s.index;
        for (i; i <= vestingPoints.length - 1; i++) {
            if (block.timestamp >= vestingPoints[i][0]) {
                pctAmount += (s.userDeposit * vestingPoints[i][1]) / 10000;
            } else {
                break;
            }
        }
        if (pctAmount != 0) {
            if (address(token) == address(1)) {
                (bool sent, ) = payable(sender).call{value: pctAmount}("");
                require(sent, "Failed to send BNB to receiver");
            } else {
                token.safeTransfer(sender, pctAmount);
            }
            s.index = uint128(i);
            s.amountClaimed += pctAmount;
        }
    }
```

At the end of the function, the user's `UserDetails.index` is assigned to the last claimed checkpoint of the vesting points array. The issue is that the function updates the last claimed index *AFTER* making the external call to transfer the funds. 

```solidity
if (pctAmount != 0) {
    if (address(token) == address(1)) {
@>      (bool sent, ) = payable(sender).call{value: pctAmount}(""); // @audit external call before index update
        require(sent, "Failed to send BNB to receiver");
    } else {
        token.safeTransfer(sender, pctAmount);
    }
@>  s.index = uint128(i);
    s.amountClaimed += pctAmount;
}
```

What this allows is the receiving user to re-enter into the claim function by calling the contract again from their `fallback()` function, before the index is updated and essentially drain the whole contract.
## Impact
Critical severity as it allows a user to drain the contract of all funds. The contract stores funds for multiple users at a time, which can be seen from the update function which allows the admins to push funds for up to 250 users at one time.

```solidity
    function updateUserDeposit(
        address[] memory _users,
        uint256[] memory _amount
    ) public onlyRole(DEFAULT_ADMIN_ROLE) {
        require(_users.length <= 250, "array length should be less than 250");
        ...
        token.safeTransferFrom(distributionWallet, address(this), amount);
```
## Code Snippet
```solidity
if (pctAmount != 0) {
    if (address(token) == address(1)) {
@>      (bool sent, ) = payable(sender).call{value: pctAmount}(""); // @audit external call before index update
        require(sent, "Failed to send BNB to receiver");
    } else {
        token.safeTransfer(sender, pctAmount);
    }
@>  s.index = uint128(i);
    s.amountClaimed += pctAmount;
}
```
## Tool used
Manual Review
## Recommendation
Follow proper CEI pattern - update the index before the external call. Can add `nonReentrant` modifier as well.