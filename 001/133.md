Melodic Garnet Hamster

high

# `Vesting.sol::claim()` does not follow CEI making the function susceptible to Reentrancy Attacks.

## Summary

`Vesting.sol` offers a function for users to claim `pctAmount`. The function does not follow `Checks-Effects-Interactions` pattern and creates an opportunity for malicious users to claim more than intended `pctAmount`.

## Vulnerability Detail

The vulnerability exists due to state variables being updated after external calls have been made. The `claim()` calculates the amount earned by Users for all vestingPoints which are open using the following line of code

```solidity
pctAmount += (s.userDeposit * vestingPoints[i][1]) / 10000;
```

The next if condition checks if `pctAmount != 0` and then checks `if (address(token) == address(1)` (native token), Following the if condition, the code executes an external call to the sender with the pctAmount set as value.

```solidity
if (pctAmount != 0) {
            if (address(token) == address(1)) {
                (bool sent, ) = payable(sender).call{value: pctAmount}("");
                require(sent, "Failed to send BNB to receiver");
            } else {
                token.safeTransfer(sender, pctAmount);
            }
            s.index = uint128(i);
            s.amountClaimed += pctAmount;
        }
```

The external call forwards control to the callers receive function through which they can call the claim function again and start accumulating pctAmounts. `token.safeTransfer` is also written before updating state variables.

The state variable `s.index` is set to the current index of claimed vestings, which makes sure that Users can not claim again if they have already claimed upto the latest opened vesting. Updating this after making the external calls makes this function susceptible to Reentrancy Attack. 

## Impact

Users can call the `claim()` function repeatedly to increase their `amountClaimed` more than what they are intended to receive.

## Code Snippet

[`Vesting.sol::claim()`](https://github.com/sherlock-audit/2024-03-zap-protocol/blob/main/zap-contracts-labs/contracts/Vesting.sol#L67-L92)

## Tool used

Manual Review

## Recommendation

Consider updating the state variables before making external calls
```diff
function claim() external {
        address sender = msg.sender;

        UserDetails storage s = userdetails[sender];
        require(s.userDeposit != 0, "No Deposit");
        require(s.index != vestingPoints.length, "already claimed");
        uint256 pctAmount;
        uint256 i = s.index;
        for (i; i <= vestingPoints.length - 1; i++) {
            if (block.timestamp >= vestingPoints[i][0]) {
                pctAmount += (s.userDeposit * vestingPoints[i][1]) / 10000;
            } else {
                break;
            }
        }
        if (pctAmount != 0) {
+           s.index = uint128(i);
+           s.amountClaimed += pctAmount;
            if (address(token) == address(1)) {
                (bool sent, ) = payable(sender).call{value: pctAmount}("");
                require(sent, "Failed to send BNB to receiver");
            } else {
                token.safeTransfer(sender, pctAmount);
            }
-           s.index = uint128(i);
-           s.amountClaimed += pctAmount;
        }
    }
```
