Jumpy Violet Ram

high

# Unexpected behaviour because `amount` is multiplied by a wrong value in `TokenSale._processPrivate()`

## Summary
The amount of USDC in [`TokenSale.processPrivate()`](https://github.com/sherlock-audit/2024-03-zap-protocol/blob/c2ad35aa844899fa24f6ed0cbfcf6c7e611b061a/zap-contracts-labs/contracts/TokenSale.sol#L221) is multiplied by PCT_BASE. USDC has `6 decimals` and PCT_BASE is `1e18`, so the result will have `24 decimals`. The amount is expected to be in 18 decimals. Because of this discrepancy core contract functionalities will be broken.

## Vulnerability Detail
When a TokenSale is [`initialized`](https://github.com/sherlock-audit/2024-03-zap-protocol/blob/c2ad35aa844899fa24f6ed0cbfcf6c7e611b061a/zap-contracts-labs/contracts/TokenSale.sol#L80), its params are set.

```solidity
    function initialize(
        Params calldata _params,
        address _stakingContract,
        address _admin,
        uint256 _maxAllocation,
        uint256 _globalTaxRate,
        bool _isKYC,
        uint256 _whitelistTxRate
    ) external initializer {
        params = _params;
        ...
        state.totalSupplyInValue = uint128(
            (uint256(_params.totalSupply) *
                uint256(_params.privateTokenPrice)) / 10 ** 18
    }
```

In the [definition of `Params`](https://github.com/sherlock-audit/2024-03-zap-protocol/blob/c2ad35aa844899fa24f6ed0cbfcf6c7e611b061a/zap-contracts-labs/contracts/interfaces/ITokenSale.sol#L32-L37) we can see that `totalSupply` and `privateTokenPrice` are meant to have 18 decimals. `state.totalSupplyInValue` will also have 18 decimals.

```solidity
    struct Params {
        uint96 totalSupply; //MUST BE 10**18;
        uint32 privateStart;
        uint96 privateTokenPrice; // MUST BE 10**18 in $
        uint32 privateEnd;
    }
```

When a user calls [`TokenSale.deposit()`](https://github.com/sherlock-audit/2024-03-zap-protocol/blob/c2ad35aa844899fa24f6ed0cbfcf6c7e611b061a/zap-contracts-labs/contracts/TokenSale.sol#L162-L184), the calculations are handled by [`_processPrivate`](https://github.com/sherlock-audit/2024-03-zap-protocol/blob/c2ad35aa844899fa24f6ed0cbfcf6c7e611b061a/zap-contracts-labs/contracts/TokenSale.sol#L217-L253).  It multiplies the USDC amount (`6 decimals`) by PCT_BASE (`18 decimals`) = `24 decimals`.

```solidity
        uint256 amount = _amount * PCT_BASE;
        uint256 sum = s.amount + amount;
```

Adding amount to sum means that sum will also have 24 decimals. The sum is compared to `maxAllocationOfUser` which has 18 decimals. As a result, many deposits will fail since the sum will be a much bigger value than expected.

```solidity
        uint256 maxAllocationOfUser = (calculateMaxAllocation(_sender)) *
            PCT_BASE;
        require(sum <= maxAllocationOfUser, "upto max allocation");
```

For the deposits that won't fail the problem doesn't end here. The amount is used for the following two variables
```solidity
        s.amount += uint128(amount);
        state.totalPrivateSold += uint128(amount);
```
This will cause all related contract functionality to behave in unexpected ways. For example, in _claim `totalPrivateSold` (24 decimals) will be greater than `totalSupplyInValue` (18 decimals) and the `left` variable will be left with default value of 0

```solidity
    function _claim(Staked memory _s) internal view returns (uint120, uint256) {
        uint256 left;
        if (state.totalPrivateSold > (state.totalSupplyInValue)) {
            uint256 rate = (state.totalSupplyInValue * PCT_BASE) /
                state.totalPrivateSold;
            _s.share = uint120((uint256(_s.amount) * rate) / PCT_BASE); 
            left = uint256(_s.amount) - uint256(_s.share);
        } else {
            _s.share = uint120(_s.amount);
        }

        return (_s.share, left);
    }
``` 

## Impact
Most deposits will fail. The contract will behave unexpectedly for these that won't.

## Code Snippet
```solidity
    function deposit(uint256 _amount) external {
        console.log("kyc enabled", isKYCEnabled);
        if (isKYCEnabled) {
            require(admin.isKYCDone(msg.sender) == true, "KYC not done");
        }
        address sender = msg.sender;
        require(
            !admin.blacklist(address(this), sender),
            "TokenSale: Blacklisted"
        );
        checkingEpoch();

        require(epoch == Epoch.Private, "TokenSale: Incorrect time");
        require(_amount > 0, "TokenSale: 0 deposit");

        if (userDepositIndex[sender] == 0) {
            usersOnDeposit.push(sender);
            userDepositIndex[sender] = usersOnDeposit.length;
        }
        if (epoch == Epoch.Private) {
            _processPrivate(sender, _amount);
        }
    }
```

```solidity
    function _processPrivate(address _sender, uint256 _amount) internal {
        require(_amount > 0, "TokenSale: Too small");

        Staked storage s = stakes[_sender];
        uint256 amount = _amount * PCT_BASE;
        uint256 sum = s.amount + amount;

        uint256 maxAllocationOfUser = (calculateMaxAllocation(_sender)) *
            PCT_BASE;
        require(sum <= maxAllocationOfUser, "upto max allocation");
        uint256 taxFreeAllcOfUser = 0; // hardcode zero - all pools have ax

        uint256 userTaxAmount;

        if (sum > taxFreeAllcOfUser) {
            uint256 userTxRate = userTaxRate(sum, _sender);
            if (s.amount < taxFreeAllcOfUser) {
                userTaxAmount =
                    ((sum - taxFreeAllcOfUser) * userTxRate) /
                    POINT_BASE;
            } else {
                userTaxAmount = (amount * userTxRate) / POINT_BASE;
            }
        }

        if (userTaxAmount > 0) {
            s.taxAmount += userTaxAmount;
            usdc.safeTransferFrom(_sender, marketingWallet, userTaxAmount);
        }
        s.amount += uint128(amount);
        state.totalPrivateSold += uint128(amount);
        usdc.safeTransferFrom(_sender, address(this), amount);

        /**@notice Forbid unstaking*/
        // stakingContract.setPoolsEndTime(_sender, uint256(params.privateEnd)); // TODO: uncomment
        emit DepositPrivate(_sender, _amount, address(this));
    }
```

## Tool used

Manual Review

## Recommendation
Multiply the amount by `1e12` instead of `PCT_BASE` .

```diff
-   uint256 amount = _amount * PCT_BASE;
+  uint256 amount = _amount * 1e12;
```
