Lively Sandstone Vulture

medium

# `SafeERC20` is imported but not used.

## Summary

ZapLaunches' [`Admin`](https://github.com/sherlock-audit/2024-03-zap-protocol/blob/main/zap-launches-contracts/contracts/Admin.sol) contract attempts to [`transferFrom`](https://github.com/sherlock-audit/2024-03-zap-protocol/blob/c2ad35aa844899fa24f6ed0cbfcf6c7e611b061a/zap-launches-contracts/contracts/Admin.sol#L310) but ignores the returned boolean `success` result, making it oblivious to unsuccessful token transfers which do not result in a `revert`.

## Vulnerability Detail

Although [`Admin`](https://github.com/sherlock-audit/2024-03-zap-protocol/blob/main/zap-launches-contracts/contracts/Admin.sol) includes OpenZeppelin's [`SafeERC20`](https://github.com/OpenZeppelin/openzeppelin-contracts/blob/master/contracts/token/ERC20/utils/SafeERC20.sol) library and associates it with the [`IERC20D`](https://github.com/sherlock-audit/2024-03-zap-protocol/blob/main/zap-launches-contracts/contracts/interfaces/IERC20D.sol) interface, this does not mean it takes effect when actually executing transfers:

```solidity
contract Admin is AccessControl, Initializable {
    using SafeERC20 for IERC20D;
```

During a call to [`createPoolNew`](https://github.com/sherlock-audit/2024-03-zap-protocol/blob/c2ad35aa844899fa24f6ed0cbfcf6c7e611b061a/zap-launches-contracts/contracts/Admin.sol#L288C15-L288C28), [`Admin`](https://github.com/sherlock-audit/2024-03-zap-protocol/blob/main/zap-launches-contracts/contracts/Admin.sol) attempts to `transferFrom` but does not check the success response:

```solidity
if(!creators[msg.sender]) creators[msg.sender] = true;
@> IERC20D(_params.tokenAddress).transferFrom(msg.sender, instance, (_params.totalSupply + _params.tokenLiquidity)); /// @audit unechecked
(bool suc, ) = payable(wallet).call{value: msg.value}("");         
```

This makes the operation insensitive to non-halting failures of ERC-20 token transfers.

## Impact

This issue usually qualifies as medium severity:
https://github.com/sherlock-audit/2023-04-footium-judging/issues/379
https://github.com/sherlock-audit/2023-12-notional-update-5-judging/issues/10

## Code Snippet

```solidity
function createPoolNew(
    ITokenSale.Params memory _params,
    ITokenSale.Config memory _config,
    uint256 _maxAllocation,
    bool _isKYC,
    bool isETHBased
) external payable {
    require(msg.value == platformFee,"Invalid platform Fee");
    _checkingParams(_params, _config);
    
    address instance = isETHBased ? Clones.clone(masterTokenSaleETH) : Clones.clone(masterTokenSaleUSDB);
    params[instance] = _params;
    ITokenSale(instance).initialize(
        _params,
        address(this),
        msg.sender,
        _maxAllocation,
        platformTax,
        _isKYC
    );
    ITokenSale(instance).setConfig(_config, msg.sender);
    if(!creators[msg.sender]) creators[msg.sender] = true;
    IERC20D(_params.tokenAddress).transferFrom(msg.sender, instance, (_params.totalSupply + _params.tokenLiquidity));
    (bool suc, ) = payable(wallet).call{value: msg.value}("");             
    require(suc, "platformFee trn failed");
    _addToSales(instance);
    emit CreateTokenSale(instance);
}
```

## Tool used

Manual Review

## Recommendation

It is recommended that implementors always use `safeTransfer` and `safeTransferFrom`:

```diff
if(!creators[msg.sender]) creators[msg.sender] = true;
- IERC20D(_params.tokenAddress).transferFrom(msg.sender, instance, (_params.totalSupply + _params.tokenLiquidity));
+ IERC20D(_params.tokenAddress).safeTransferFrom(msg.sender, instance, (_params.totalSupply + _params.tokenLiquidity));
(bool suc, ) = payable(wallet).call{value: msg.value}("");             
```
