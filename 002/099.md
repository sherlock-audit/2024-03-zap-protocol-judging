Skinny Cobalt Rooster

high

# Lack of access control on the `destroyInstance` function can lead to unauthorized destruction of tokenSale contracts.

## Summary
IDOs can be destroyed by anyone who calls the `destroyInstance` function in Admin.sol contract due to the lack of access control.

## Vulnerability Detail
The token sale instances can be destroyed in two ways, through the `destroyInstance` function in the Admin.sol contract or through the `destroy` function in the TokenSale contract. 
The `destroyInstance` function goes like this;

```solidity
    function destroyInstance(
        address _instance
    ) external onlyExist(_instance) onlyIncoming(_instance) {
        _removeFromSales(_instance);
        ITokenSale(_instance).destroy();
    }
```
and as can be seen from the function, there's no check for whether the caller is authorized, just as the internal `_removeFromSales` function doesn't check for the caller.

```solidity
    function _removeFromSales(address _addr) internal {
        tokenSalesM[_addr] = false;
        tokenSales[indexOfTokenSales[_addr]] = tokenSales[
            tokenSales.length - 1
        ];
        indexOfTokenSales[
            tokenSales[tokenSales.length - 1]
        ] = indexOfTokenSales[_addr];
        tokenSales.pop();
        delete indexOfTokenSales[_addr];
    }
```
The function then forwards the call to the `destroy` function in the tokensale contract, which is protected by the `_onlyAdmin` internal function.

```solidity
    function destroy() external override {
        _onlyAdmin();
        uint256 amountUSDC = usdc.balanceOf(address(this));
        if (amountUSDC > 0) {
            usdc.safeTransfer(admin.wallet(), amountUSDC);
        }
        address payable wallet = payable(admin.wallet());
        selfdestruct(wallet);
    }
```
While this might seem like a good enough form of access control, the function goes like this;

```solidity
    function _onlyAdmin() internal view {
        require(
            admin.hasRole(DEFAULT_ADMIN_ROLE, msg.sender) ||
                msg.sender == address(admin),
            "TokenSale: Onlyadmin"
        );
    }
```
which shows that it the caller must either be the `DEFAULT_ADMIN_ROLE` or the admin contract. This allows that calls to the `destroy` function originating from the Admin.sol contract will be successfully executed.
Hence, anyone who calls the `destroyInstance` function will be able to destroy any TokenSale.
## Impact
Unauthorized destruction of TokenSale contracts, can be used for any/all forms of different griefing attacks.
## Code Snippet

https://github.com/sherlock-audit/2024-03-zap-protocol/blob/c2ad35aa844899fa24f6ed0cbfcf6c7e611b061a/zap-contracts-labs/contracts/Admin.sol#L138

https://github.com/sherlock-audit/2024-03-zap-protocol/blob/c2ad35aa844899fa24f6ed0cbfcf6c7e611b061a/zap-contracts-labs/contracts/TokenSale.sol#L186C2-L194C6

https://github.com/sherlock-audit/2024-03-zap-protocol/blob/c2ad35aa844899fa24f6ed0cbfcf6c7e611b061a/zap-contracts-labs/contracts/TokenSale.sol#L151C1-L158C1

## Tool used

Manual Review

## Recommendation
Add the onlyAdmin or onlyOperator modifier to the `destroyInstance` function.
```solidity
    function destroyInstance(
        address _instance
    ) external onlyExist(_instance) onlyIncoming(_instance) { //@note add onlyAdmin || onlyRole(OPERATOR) modifier here
        _removeFromSales(_instance);
        ITokenSale(_instance).destroy();
    }
```
