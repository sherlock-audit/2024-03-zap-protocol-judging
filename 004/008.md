Lively Scarlet Alpaca

high

# Users may receive a greater or less tax refund if the `userTaxRate` is altered before they claim it

## Summary

The `userTaxRate` may be altered before users receive their additional taxes back, potentially resulting in users receiving more or less taxes than they are entitled to.

## Vulnerability Detail

There are two different `userTaxRate` for whitelisted users and non-whitelisted users. 

```solidity
    function userTaxRate(
        uint256 _amount,
        address _sender
    ) public returns (uint256) {
        uint256 userTaxFreeAllc = 0;

        if (_amount > userTaxFreeAllc) {
            if (isWhitelisted[_sender]) {
                return whitelistTxRate;
            } else {
                return globalTaxRate;
            }
        } else {
            return 0;
        }
    }
```

When users invest in the private round, the taxes are calculated based on the newest `userTaxRate`(`whitelistTxRate` or `globalTaxRate`).

```solidity
    function _processPrivate(address _sender, uint256 _amount) internal {
        require(_amount > 0, "TokenSale: Too small");

        Staked storage s = stakes[_sender];
        uint256 amount = _amount * PCT_BASE;
        uint256 sum = s.amount + amount;

        uint256 maxAllocationOfUser = (calculateMaxAllocation(_sender)) *
            PCT_BASE;
        require(sum <= maxAllocationOfUser, "upto max allocation");
        uint256 taxFreeAllcOfUser = 0; // hardcode zero - all pools have ax

        uint256 userTaxAmount;

        if (sum > taxFreeAllcOfUser) {
//@audit the whitelisted user has their own userTxRate 
            uint256 userTxRate = userTaxRate(sum, _sender);
            if (s.amount < taxFreeAllcOfUser) {
                userTaxAmount =
                    ((sum - taxFreeAllcOfUser) * userTxRate) /
                    POINT_BASE;
            } else {
                userTaxAmount = (amount * userTxRate) / POINT_BASE;
            }
        }

        if (userTaxAmount > 0) {
            s.taxAmount += userTaxAmount;
            usdc.safeTransferFrom(_sender, marketingWallet, userTaxAmount);
        }
        s.amount += uint128(amount);
        state.totalPrivateSold += uint128(amount);
        usdc.safeTransferFrom(_sender, address(this), amount);

        /**@notice Forbid unstaking*/
        // stakingContract.setPoolsEndTime(_sender, uint256(params.privateEnd)); // TODO: uncomment
        emit DepositPrivate(_sender, _amount, address(this));
    }
```

When refunding the taxes, the `tax` is also the newest value of `userTaxRate`.

```solidity
    function claim() external {
        checkingEpoch();
        require(
            uint8(epoch) > 1 && !admin.blockClaim(address(this)),
            "TokenSale: Not time or not allowed"
        );

        Staked storage s = stakes[msg.sender];
        require(s.amount != 0, "TokenSale: No Deposit");
        require(!s.claimed, "TokenSale: Already Claimed");

        uint256 left;
        (s.share, left) = _claim(s);
        require(left > 0, "TokenSale: Nothing to claim");
        uint256 refundTaxAmount;
        if (s.taxAmount > 0) {
            uint256 tax = userTaxRate(s.amount, msg.sender);
            uint256 taxFreeAllc = _maxTaxfreeAllocation(msg.sender) * PCT_BASE;
            if (taxFreeAllc >= s.share) {
                refundTaxAmount = s.taxAmount;
            } else {
                refundTaxAmount = (left * tax) / POINT_BASE;
            }
            usdc.safeTransferFrom(marketingWallet, msg.sender, refundTaxAmount);
        }
        s.claimed = true;
        usdc.safeTransfer(msg.sender, left);
        emit Claim(msg.sender, left);
    }
```

This situation may lead to issues that could affect the core functionality of the contract:

**Case:**
Let's consider the following parameters:
`whitelistTxRate` = 0.5
`globalTaxRate` = 0.1

1. Alice deposits 10 USDC with the `userTaxRate` set to `globalTaxRate` = 0.1, resulting in a `taxAmount` of 1 USDC.
2. Alice is subsequently added to the whitelist, with the `userTaxRate` now matching `whitelistTxRate` = 0.5.
3. After the private round, Alice retains 4 USDC. Consequently, when she claims her USDC and requests the extra taxes back, the `refundTaxAmount` is calculated as 4 * 0.5 = 2 USDC.
4. Alice originally paid 1 USDC in tax but received a tax refund of 2 USDC.


## Impact

Users may get an unexpected amount of taxes back if the `userTaxRate` is changed before they claim the remaining usdc.

## Code Snippet

https://github.com/sherlock-audit/2024-03-zap-protocol/blob/c2ad35aa844899fa24f6ed0cbfcf6c7e611b061a/zap-contracts-labs/contracts/TokenSale.sol#L320-L335
https://github.com/sherlock-audit/2024-03-zap-protocol/blob/c2ad35aa844899fa24f6ed0cbfcf6c7e611b061a/zap-contracts-labs/contracts/TokenSale.sol#L217-L253
https://github.com/sherlock-audit/2024-03-zap-protocol/blob/c2ad35aa844899fa24f6ed0cbfcf6c7e611b061a/zap-contracts-labs/contracts/TokenSale.sol#L364-L392


## Tool used

Manual Review

## Recommendation

It's recommended to record the taxRate that is used during the private round and use this taxRate to calculate the amount of tax refund.