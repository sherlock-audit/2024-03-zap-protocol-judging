Lively Scarlet Alpaca

high

# The incorrect calculation of tax in the `TokenSale` contract may result in users paying excessive taxes

## Summary


During the private round, the `taxFreeAllcOfUser` is consistently zero, indicating that users are required to pay taxes on the entire investment amount. However, when users attempt to reclaim their tax refund, they must ensure that they have usdc left after private round. Otherwise, even if the investment amount is less than `taxFreeAllc`, they still have to pay the taxes. 


## Vulnerability Detail

When users invest to the private round, they must pay taxes for the whole investment amount because the `taxFreeAllcOfUser` is zero.

```solidity
    function _processPrivate(address _sender, uint256 _amount) internal {
        require(_amount > 0, "TokenSale: Too small");

        Staked storage s = stakes[_sender];
        uint256 amount = _amount * PCT_BASE;
        uint256 sum = s.amount + amount;

        uint256 maxAllocationOfUser = (calculateMaxAllocation(_sender)) *
            PCT_BASE;
        require(sum <= maxAllocationOfUser, "upto max allocation");
//@audit the taxFeeAllocOfUser is always zero, user must pay for taxes for the whole investment amount 
        uint256 taxFreeAllcOfUser = 0; // hardcode zero - all pools have ax

        uint256 userTaxAmount;

        if (sum > taxFreeAllcOfUser) {
            uint256 userTxRate = userTaxRate(sum, _sender);
            if (s.amount < taxFreeAllcOfUser) {
                userTaxAmount =
                    ((sum - taxFreeAllcOfUser) * userTxRate) /
                    POINT_BASE;
            } else {
                userTaxAmount = (amount * userTxRate) / POINT_BASE;
            }
        }

        if (userTaxAmount > 0) {
            s.taxAmount += userTaxAmount;
            usdc.safeTransferFrom(_sender, marketingWallet, userTaxAmount);
        }
        s.amount += uint128(amount);
        state.totalPrivateSold += uint128(amount);
        usdc.safeTransferFrom(_sender, address(this), amount);

        /**@notice Forbid unstaking*/
        // stakingContract.setPoolsEndTime(_sender, uint256(params.privateEnd)); // TODO: uncomment
        emit DepositPrivate(_sender, _amount, address(this));
    }
```

When the private round concludes, users have the option to claim their remaining USDC and request a tax refund. However, the `claim()` function only facilitates tax refunds when users have remaining USDC. In essence, if a user has no USDC left, they cannot retrieve their taxes, even if their share is below the `taxFreeAllc` threshold.

```solidity
    function claim() external {
        checkingEpoch();
        require(
            uint8(epoch) > 1 && !admin.blockClaim(address(this)),
            "TokenSale: Not time or not allowed"
        );

        Staked storage s = stakes[msg.sender];
        require(s.amount != 0, "TokenSale: No Deposit");
        require(!s.claimed, "TokenSale: Already Claimed");

        uint256 left;
        (s.share, left) = _claim(s);
        require(left > 0, "TokenSale: Nothing to claim");
        uint256 refundTaxAmount;
        if (s.taxAmount > 0) {
            uint256 tax = userTaxRate(s.amount, msg.sender);
            uint256 taxFreeAllc = _maxTaxfreeAllocation(msg.sender) * PCT_BASE;
            if (taxFreeAllc >= s.share) {
                refundTaxAmount = s.taxAmount;
            } else {
                refundTaxAmount = (left * tax) / POINT_BASE;
            }
            usdc.safeTransferFrom(marketingWallet, msg.sender, refundTaxAmount);
        }
        s.claimed = true;
        usdc.safeTransfer(msg.sender, left);
        emit Claim(msg.sender, left);
    }
```

For instance, suppose Alice invested 10 USDC in the private round, resulting in a tax of 10 * 0.1 = 1 USDC. Although Alice has no remaining USDC after the private round, her `taxFreeAllc` is 20 USDC. Alice should be entitled to receive the 1 USDC tax refund since her 10 USDC investment is below the `taxFreeAllc`. However, this is not currently possible due to Alice having no USDC left.


## Impact

Users incur losses of funds as they are unable to reclaim their taxes, even if their share falls below the `taxFreeAllc` threshold.

## Code Snippet

https://github.com/sherlock-audit/2024-03-zap-protocol/blob/c2ad35aa844899fa24f6ed0cbfcf6c7e611b061a/zap-contracts-labs/contracts/TokenSale.sol#L217-L253
https://github.com/sherlock-audit/2024-03-zap-protocol/blob/c2ad35aa844899fa24f6ed0cbfcf6c7e611b061a/zap-contracts-labs/contracts/TokenSale.sol#L364-L392


## Tool used

Manual Review

## Recommendation

Users should be allowed to refund their taxes even if they have no USDC left.