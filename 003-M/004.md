Jumpy Violet Ram

medium

# Claiming native tokens in `Vesting` does not work

## Summary
An admin has to call [`Vesting.updateUserDeposit()`](https://github.com/sherlock-audit/2024-03-zap-protocol/blob/c2ad35aa844899fa24f6ed0cbfcf6c7e611b061a/zap-contracts-labs/contracts/Vesting.sol#L53-L65) to set the  claiming configuration. Then users can call [`Vesting.claim()`](https://github.com/sherlock-audit/2024-03-zap-protocol/blob/c2ad35aa844899fa24f6ed0cbfcf6c7e611b061a/zap-contracts-labs/contracts/Vesting.sol#L67-L92) and claim their tokens. The tokens to be claimed can also be a native token by setting the `token` storage variable to `address(0x1)`. When the token is set to `address(0x1)`, `Vesting.updateUserDeposit()` calls safeTransferFrom on it which results in a revert that stops the admin from configuring the vesting and the user - from claiming.

## Vulnerability Detail
When `Vesting.claim()` is called, a deposit different from 0 is required.
```solidity
        UserDetails storage s = userdetails[sender];
        require(s.userDeposit != 0, "No Deposit");
```
The only place where this deposit is set is in `Vesting.updateUserDeposit()`. Look at the last line of the code. It calls `safeTransferFrom` on the `token` storage variable.

```solidity
    function updateUserDeposit(
        address[] memory _users,
        uint256[] memory _amount
    ) public onlyRole(DEFAULT_ADMIN_ROLE) {
        require(_users.length <= 250, "array length should be less than 250");
        require(_users.length == _amount.length, "array length should match");
        uint256 amount;
        for (uint256 i = 0; i < _users.length; i++) {
            userdetails[_users[i]].userDeposit = _amount[i];
            amount += _amount[i];
        }
        token.safeTransferFrom(distributionWallet, address(this), amount);
    }
```

As we can see in the claiming function, the token set to `address(1)` means that native token payments will take place.
```solidity
            if (address(token) == address(1)) {
                (bool sent, ) = payable(sender).call{value: pctAmount}("");
                require(sent, "Failed to send BNB to receiver");
            } else {
                token.safeTransfer(sender, pctAmount);
            }
```

There is nothing deployed at `address(0x1)`, so the call opcode will return `true` with no return data. In such cases, the OpenZeppelin's implementation requires that the called address is a contract. Therefore, a `revert` will always happen.

```solidity
        if (success) {
            if (returndata.length == 0) {
                // only check isContract if the call was successful and the return data is empty
                // otherwise we already know that it was a contract
                require(isContract(target), "Address: call to non-contract");
            }
            return returndata;
        }
```
## Impact
Broken core protocol functionality.

## Code Snippet
<details>

<summary>Foundry PoC</summary>


```solidity
// SPDX-License-Identifier: UNLICENSED
pragma solidity ^0.8.11;

import "forge-std/Test.sol";
import "../contracts/Vesting.sol";
import "../contracts/VestingFactory.sol";
import "@openzeppelin/contracts/token/ERC20/IERC20.sol";

contract VestingTest is Test {
     VestingFactory public factory;
     Vesting public vestingMaster;

    function setUp() public {
        vestingMaster = new Vesting();
        factory = new VestingFactory(address(vestingMaster));
        factory.setOperator(address(this));
    }

    function testVesting() public {
        uint128[2][] memory vestingPoints = _getVestingPoints();
        Vesting vestingContract = _deployVesting(address(0x1), address(this), vestingPoints);
       
        vm.expectRevert("Address: call to non-contract");
        _updateDeposits(vestingContract);
    }

    function _updateDeposits(Vesting _vestingContract) public {
        address[] memory users = new address[](1);
        uint256[] memory amounts = new uint256[](1);

        users[0] = address(0xb0b);
        amounts[0] = 2 ether;
        
        _vestingContract.updateUserDeposit(users, amounts);
    }

    function _deployVesting(address _token, address _wallet, uint128[2][] memory _vestingPoints) internal returns (Vesting){
        factory.createVestingSchedule(address(0), IERC20(_token), _wallet, _vestingPoints);
        return Vesting(0xffD4505B3452Dc22f8473616d50503bA9E1710Ac);
    }

    function _getVestingPoints() internal returns(uint128[2][] memory) {
        uint128[2][] memory vestingPoints = new uint128[2][](1);
        uint128[2] memory points;

        points[0] = uint128(block.timestamp);
        points[1] = 10000;

        vestingPoints[0] = points;

        return vestingPoints;
    } 
}
```

</details>


## Tool used

Foundry

## Recommendation
Execute the token transfer only if it's not a native token.

```diff
    function updateUserDeposit(
        address[] memory _users,
        uint256[] memory _amount
    ) public onlyRole(DEFAULT_ADMIN_ROLE) {
        require(_users.length <= 250, "array length should be less than 250");
        require(_users.length == _amount.length, "array length should match");
        uint256 amount;
        for (uint256 i = 0; i < _users.length; i++) {
            userdetails[_users[i]].userDeposit = _amount[i];
            amount += _amount[i];
        }
+     if (address(token) != address(0x1)) {
               token.safeTransferFrom(distributionWallet, address(this), amount);
+      }
    }
```
