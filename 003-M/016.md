Jumpy Violet Ram

medium

# `Vesting` contract cannot receive native token

## Summary
[Vesting.sol](https://github.com/sherlock-audit/2024-03-zap-protocol/blob/main/zap-contracts-labs/contracts/Vesting.sol) cannot receive native tokens (ETH) because of missing `receive` function.

## Vulnerability Detail
In [Vesting.claim](https://github.com/sherlock-audit/2024-03-zap-protocol/blob/c2ad35aa844899fa24f6ed0cbfcf6c7e611b061a/zap-contracts-labs/contracts/Vesting.sol#L83C1-L86C15) if the token is set to `address(1)`, the contract sends `pctAmount` to the `sender`. However, the contract does not have neither `payable` functions, nor `receive/fallback` functions, so there is no way to fund the contract and the vesting will not work.

```solidity
            if (address(token) == address(1)) {
                (bool sent, ) = payable(sender).call{value: pctAmount}("");
                require(sent, "Failed to send BNB to receiver");
            } 
```

## Impact
Broken native token vesting

## Code Snippet
A Foundry PoC
```solidity
// SPDX-License-Identifier: UNLICENSED
pragma solidity ^0.8.11;

import "forge-std/Test.sol";
import "../contracts/Vesting.sol";
import "../contracts/VestingFactory.sol";
import "@openzeppelin/contracts/token/ERC20/IERC20.sol";

contract VestingTest is Test {
     VestingFactory public factory;
     Vesting public vestingMaster;

    function setUp() public {
        vestingMaster = new Vesting();
        factory = new VestingFactory(address(vestingMaster));
        factory.setOperator(address(this));
    }

    function testFunding() public {
        deal(address(this), 2 ether);
        assertEq(address(this).balance, 2 ether);

        uint128[2][] memory vestingPoints = _getVestingPoints();
        Vesting vestingContract = _deployVesting(address(0x1), address(this), vestingPoints);

        (bool success, ) = payable(address(vestingContract)).call{value: 2 ether}("");
        assertEq(success, false);
    }

    function _deployVesting(address _token, address _wallet, uint128[2][] memory _vestingPoints) internal returns (Vesting){
        factory.createVestingSchedule(address(0), IERC20(_token), _wallet, _vestingPoints);
        return Vesting(payable(0xffD4505B3452Dc22f8473616d50503bA9E1710Ac));
    }

    function _getVestingPoints() internal returns(uint128[2][] memory) {
        uint128[2][] memory vestingPoints = new uint128[2][](1);
        uint128[2] memory points;

        points[0] = uint128(block.timestamp);
        points[1] = 10000;

        vestingPoints[0] = points;

        return vestingPoints;
    } 
}
```

## Tool used

Foundry

## Recommendation
Add a `receive` function so the contract can be funded.
```diff
+    receive() payable external {}
```