Shambolic Lace Giraffe

high

# Incorrect claim index and update

## Summary

The vesting claim function incorrectly updates the user index and may produce a leakage of value. 

## Vulnerability Detail

There are several problems with the `Vesting` claim:
```solidity
    function claim() external {
        address sender = msg.sender;

        UserDetails storage s = userdetails[sender];
        require(s.userDeposit != 0, "No Deposit");
        require(s.index != vestingPoints.length, "already claimed");
        uint256 pctAmount;
        uint256 i = s.index;
        for (i; i <= vestingPoints.length - 1; i++) {
            if (block.timestamp >= vestingPoints[i][0]) {
                pctAmount += (s.userDeposit * vestingPoints[i][1]) / 10000;
            } else {
                break;
            }
        }
        if (pctAmount != 0) {
            if (address(token) == address(1)) {
                (bool sent, ) = payable(sender).call{value: pctAmount}("");
                require(sent, "Failed to send BNB to receiver");
            } else {
                token.safeTransfer(sender, pctAmount);
            }
            s.index = uint128(i);
            s.amountClaimed += pctAmount;
        }
    }
```
First of all, when the claim is not 0 it sets `s.index = uint128(i);`. However, if this function is invoked again before another vesting point starts, it will continue from the same index (in this case, 0), so users can repeatedly claim their allocations.
E.g. Claim `i = 0`  -> `s.index = 0`. Repeat.

Another possible problem is that the admin can update the vesting points anytime:
```solidity
    function updateVestingPoints(
        uint128[2][] memory _vestingPoints
    ) external onlyRole(DEFAULT_ADMIN_ROLE) {
        uint256 sum;
        (vestingPoints, sum) = ascendingSort(_vestingPoints);
        require(sum == 10000, "sum not 10000");
        require(block.timestamp <= _vestingPoints[0][0], "Time lapsed");
    }
```
If the vesting schedule changes, it will impact users who have already claimed some points. Depending on the values of new `vestingPoints` it will lead to users getting more or less than initially anticipated. Tokens are transferred from `distributionWallet` when storing user deposits. There could not be enough tokens to cover new vesting points after changes. I believe current `vestingPoints` should be attached to the user struct (`UserDetails`) when tokens are deposited for vesting (`updateUserDeposit`).

## Impact

Incorrect index setting allows claiming allocation for the same index multiple times.

Vesting points change will lead to malfunctioning allocations of existing users.

## Code Snippet

https://github.com/sherlock-audit/2024-03-zap-protocol/blob/main/zap-contracts-labs/contracts/Vesting.sol#L89

https://github.com/sherlock-audit/2024-03-zap-protocol/blob/main/zap-contracts-labs/contracts/Vesting.sol#L98

## Tool used

Manual Review

## Recommendation
1) Set `s.index` to `i+1`.
2) Cache `vestingPoints` to `UserDetails` upon `updateUserDeposit`.
