Noisy Watermelon Otter

high

# Potential Reentrancy attack on Vesting.sol

## Summary
The `Vesting.sol` contains a potential reentrancy vulnerability due to the state updates occurring after external calls within the `Vesting.sol::claim` function. This could lead to the complete withdrawal of the entire token from the contract

## Vulnerability Detail
[Link to code](https://github.com/sherlock-audit/2024-03-zap-protocol/blob/main/zap-contracts-labs/contracts/Vesting.sol#L67C5-L92C6)
The function `Vesting.sol::claim` sends out funds or tokens before state updates. However, the external calls (`payable(sender).call{value: pctAmount}("")` for sending ETH and `token.safeTransfer(sender, pctAmount)` for ERC-20 tokens) are made before updating the internal state variables `s.index` and `s.amountClaimed` which are used to check if the output is correct. This order of operations exposes the function to reentrancy attacks, where an attacker can repeatedly call `claim` before the initial call is completed, potentially leading to incorrect withdrawals.

## Impact
attack scenario

1. First, the attacker needs to get his contract on the list of participants. The list of participants is assigned by the contract admin in the function below. Since these are public vestings that are planned to be held on the platform, getting here is not a specific task. Moreover, we see that there is no check that it is user[i] who contributed amount[i], so it is enough for an attacker to get a malicious contract into the list with a non-zero amount. 
```js
function updateUserDeposit(
        address[] memory _users,
        uint256[] memory _amount
    ) public onlyRole(DEFAULT_ADMIN_ROLE) {
        require(_users.length <= 250, "array length should be less than 250");
        require(_users.length == _amount.length, "array length should match");
        uint256 amount;
        for (uint256 i = 0; i < _users.length; i++) {
            userdetails[_users[i]].userDeposit = _amount[i];
            amount += _amount[i];
        }
        token.safeTransferFrom(distributionWallet, address(this), amount);
    }
```
2. After the attacker has gotten the malicious contract to the list of participants with a non-zero amount.  In the external function claim() we see that the state change occurs after the transaction control is transferred, which will allow the attacker contract to withdraw all funds from the contract.

Let's take a closer look. The claim function can be broken down into several logical parts
- Valid output check:
```js.
address sender = msg.sender;
UserDetails storage s = userdetails[sender];
require(s.userDeposit != 0, "No Deposit");
require(s.index != vestingPoints.length, "already claimed");
```

- Counting the number of withdrawals (depends on the user's deposit size, and vestingPoints (specific to each individual vesting, but obviously not a null variable).
```js
uint256 pctAmount;
uint256 i = s.index;
for (i; i <= vestingPoints.length - 1; i++) {
        if (block.timestamp >= vestingPoints[i][0]) {
            pctAmount += (s.userDeposit * vestingPoints[i][1]) / 10000;
        } else {
            break;
        }
}
```

- withdrawal of funds. We see that either call or safeTransfer from safeERC20(Open Zeppelin) is used. Both methods are subject to reentrancy.  Changing variables that affect the validity of the output happens already after reentrancy calls. 
```js
if (pctAmount != 0) {
        if (address(token) == address(1)) {
            (bool sent, ) = payable(sender).call{value: pctAmount}("");
            require(sent, "Failed to send BNB to receiver");
        } else {
            token.safeTransfer(sender, pctAmount);
        }
        s.index = uint128(i);
        s.amountClaimed += pctAmount;
 }
```

## Code Snippet

```js
    function claim() external {
        address sender = msg.sender;

        UserDetails storage s = userdetails[sender];
        require(s.userDeposit != 0, "No Deposit");
        require(s.index != vestingPoints.length, "already claimed");
        uint256 pctAmount;
        uint256 i = s.index;
        for (i; i <= vestingPoints.length - 1; i++) {
            if (block.timestamp >= vestingPoints[i][0]) {
                pctAmount += (s.userDeposit * vestingPoints[i][1]) / 10000;
            } else {
                break;
            }
        }
        if (pctAmount != 0) {
            if (address(token) == address(1)) {
                (bool sent, ) = payable(sender).call{value: pctAmount}("");
                require(sent, "Failed to send BNB to receiver");
            } else {
                token.safeTransfer(sender, pctAmount);
            }
            s.index = uint128(i);
            s.amountClaimed += pctAmount;
        }
    }
```


## Tool used

Manual Review

## Recommendation

use OpenZeppelin ReentrancyGuard for this function
