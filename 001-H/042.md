Cheesy Menthol Walrus

high

# Adversary can claim all native ETH from the vesting contract

## Summary
`claim()` is vulnerably to reentrancy attack. Users who have vested only a small amount, can claim all ETH from the vesting contract by repeatedly entering the function.

## Vulnerability Detail

This is the implementation of `claim()`.

```javascript
function claim() external {
    address sender = msg.sender;

    UserDetails storage s = userdetails[sender];
    require(s.userDeposit != 0, "No Deposit");
    require(s.index != vestingPoints.length, "already claimed");
    uint256 pctAmount;
    uint256 i = s.index;
    for (i; i <= vestingPoints.length - 1; i++) {
        if (block.timestamp >= vestingPoints[i][0]) {
            pctAmount += (s.userDeposit * vestingPoints[i][1]) / 10000;
        } else {
            break;
        }
    }
    if (pctAmount != 0) {
        if (address(token) == address(1)) {
            (bool sent, ) = payable(sender).call{value: pctAmount}("");
            require(sent, "Failed to send BNB to receiver");
        } else {
            token.safeTransfer(sender, pctAmount);
        }
        s.index = uint128(i);
        s.amountClaimed += pctAmount;
    }
}
```

It calculates in `pctAmount` how much token/native ETH a user can claim. If native ETH is used, it sends the calculated ETH to the sender.

However, sending of `pctAmount` is done before `s.index` and `s.amountClaimed` is updated.

`msg.sender` can be a contract that implements this reentrancy attack. When `pctAmount` is received, they would call `claim()` again, reentering this function.

`s.index` and `s.amountClaimed` is still the old value used at this point. `claim()` will make the same calculation and send the same amount again to `msg.sender`. This can be done infinite times until the vesting contract is drained.

## Impact
Draining of all ETH in vesting contract by adversary.

## Code Snippet
https://github.com/sherlock-audit/2024-03-zap-protocol/blob/main/zap-contracts-labs/contracts/Vesting.sol#L67-L92

## Tool used

Manual Review

## Recommendation
Update `s.index` and `s.amountClaimed` before native ETH is sent.

