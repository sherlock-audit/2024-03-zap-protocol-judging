Lively Sandstone Vulture

high

# Vesting contract can be drained by an attacker.

## Summary

Lack of re-entrancy protection for the [`claim`](https://github.com/sherlock-audit/2024-03-zap-protocol/blob/c2ad35aa844899fa24f6ed0cbfcf6c7e611b061a/zap-contracts-labs/contracts/Vesting.sol#L67) function on the [`Vesting`](https://github.com/sherlock-audit/2024-03-zap-protocol/blob/c2ad35aa844899fa24f6ed0cbfcf6c7e611b061a/zap-contracts-labs/contracts/Vesting.sol#L67) contract allows an attacker to drain all funds.

## Vulnerability Detail

The [`claim`](https://github.com/sherlock-audit/2024-03-zap-protocol/blob/c2ad35aa844899fa24f6ed0cbfcf6c7e611b061a/zap-contracts-labs/contracts/Vesting.sol#L67) function on the [`Vesting`](https://github.com/sherlock-audit/2024-03-zap-protocol/blob/c2ad35aa844899fa24f6ed0cbfcf6c7e611b061a/zap-contracts-labs/contracts/Vesting.sol#L67) contract is susceptible to a classic re-entrancy attack:

```solidity
function claim() external { /// @audit no_reentrancy_protection
    address sender = msg.sender;

    UserDetails storage s = userdetails[sender];
    require(s.userDeposit != 0, "No Deposit");
    require(s.index != vestingPoints.length, "already claimed");
    uint256 pctAmount;
    uint256 i = s.index;
    for (i; i <= vestingPoints.length - 1; i++) {
        if (block.timestamp >= vestingPoints[i][0]) {
            pctAmount += (s.userDeposit * vestingPoints[i][1]) / 10000;
        } else {
            break;
        }
    }
    if (pctAmount != 0) {
        if (address(token) == address(1)) {
            (bool sent, ) = payable(sender).call{value: pctAmount}(""); /// @audit call_can_be_reentered
            require(sent, "Failed to send BNB to receiver");
        } else {
            token.safeTransfer(sender, pctAmount);
        }
        s.index = uint128(i); // @audit effects_after_interaction
        s.amountClaimed += pctAmount;
    }
}
```

As shown above, due to the lack of a `nonReentrant` modifier and external interactions with untrusted sources before side-effects take place (caching `s.index` and `s.amountClaimed`), an attacker can drain the contract by performing re-entrant invocations to [`claim`](https://github.com/sherlock-audit/2024-03-zap-protocol/blob/c2ad35aa844899fa24f6ed0cbfcf6c7e611b061a/zap-contracts-labs/contracts/Vesting.sol#L67) when handling an `address(token)` equal to `address(1)`.

This setup allows an attacker to repeatedly claim their vested deposit, and occurs because the intended gating mechanism of latching `s.index` to prevent double-spending a vesting iteration index does not take place until after transferring assets out of the contract (and handing execution control back over to a malicious caller).

## Impact

High.

## Code Snippet

```solidity
function claim() external {
    address sender = msg.sender;

    UserDetails storage s = userdetails[sender];
    require(s.userDeposit != 0, "No Deposit");
    require(s.index != vestingPoints.length, "already claimed");
    uint256 pctAmount;
    uint256 i = s.index;
    for (i; i <= vestingPoints.length - 1; i++) {
        if (block.timestamp >= vestingPoints[i][0]) {
            pctAmount += (s.userDeposit * vestingPoints[i][1]) / 10000;
        } else {
            break;
        }
    }
    if (pctAmount != 0) {
        if (address(token) == address(1)) {
            (bool sent, ) = payable(sender).call{value: pctAmount}("");
            require(sent, "Failed to send BNB to receiver");
        } else {
            token.safeTransfer(sender, pctAmount);
        }
        s.index = uint128(i);
        s.amountClaimed += pctAmount;
    }
}
```

## Tool used

Manual Review

## Recommendation

Use a [`nonReentrant`](https://github.com/OpenZeppelin/openzeppelin-contracts/blob/e682c7e5b5b347662868b9c8378a694626406c01/contracts/utils/ReentrancyGuard.sol#L55) modifier.

