Blunt Fleece Moose

high

# Reentrancy can drain the Vesting contract

## Summary

User can claim their vested tokens by calling `claim` in `Vesting.sol`:

```solidity
function claim() external {
        address sender = msg.sender;

        UserDetails storage s = userdetails[sender];
        require(s.userDeposit != 0, "No Deposit");
        require(s.index != vestingPoints.length, "already claimed");
        uint256 pctAmount;
        uint256 i = s.index;
        for (i; i <= vestingPoints.length - 1; i++) {
            if (block.timestamp >= vestingPoints[i][0]) {
                pctAmount += (s.userDeposit * vestingPoints[i][1]) / 10000;
            } else {
                break;
            }
        }
        if (pctAmount != 0) {
            if (address(token) == address(1)) {
                (bool sent, ) = payable(sender).call{value: pctAmount}("");
                require(sent, "Failed to send BNB to receiver");
            } else {
                token.safeTransfer(sender, pctAmount);
            }
            s.index = uint128(i);
            s.amountClaimed += pctAmount;
        }
    }
```

## Vulnerability Detail

The main defence against calling this function multiple times is this:

***`require(s.index != vestingPoints.length, "already claimed");`***

When user claims once, his s.index is set to the number of vestingPoints he has claimed for.

The vulnerability arises in this part of the code: 

```solidity
if (pctAmount != 0) {
            if (address(token) == address(1)) {
                (bool sent, ) = payable(sender).call{value: pctAmount}("");
                require(sent, "Failed to send BNB to receiver");
```

It is a problem because low-level `call` is made to the user interacting with the contract, the flow is directed towards him, so since his `s.index` is not updated yet when this happens, he can reenter `claim` therefore repeatedly draining the contract

## Impact

User can steal all ether in `Vesting.sol`.

## Code Snippet

https://github.com/sherlock-audit/2024-03-zap-protocol/blob/main/zap-contracts-labs/contracts/Vesting.sol#L67-L92

## Tool used

Manual Review

## Recommendation

Either add nonReentrant modifier to the `claim` function, update `s.index` before making the external call or do both.
